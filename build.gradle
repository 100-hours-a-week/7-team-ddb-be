plugins {
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'java'
}

group = 'com.dolpin'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17

repositories {
	mavenCentral()
}

// QueryDSL Q클래스 생성 경로 설정
def querydslGeneratedDir = file("src/main/generated")

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// QueryDSL 의존성 추가
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

	// GCS
	implementation 'com.google.cloud:google-cloud-storage:2.50.0'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

	// Database
	implementation 'org.postgresql:postgresql'
	implementation 'org.hibernate.orm:hibernate-spatial:6.6.11.Final'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// 테스트
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	//환경변수
	implementation 'me.paulschwarz:spring-dotenv:3.0.0'

	// Actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'

	// Spring Boot Test (JUnit5)
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Testcontainers
	testImplementation 'org.testcontainers:junit-jupiter:1.18.3'
	testImplementation 'org.testcontainers:postgresql:1.18.3'

	// 실제 PostgreSQL 드라이버
	testImplementation 'org.postgresql:postgresql:42.5.4'
}

// QueryDSL 설정
sourceSets {
	main {
		java {
			srcDir querydslGeneratedDir
		}
	}
}

tasks.withType(Test) {
	useJUnitPlatform()
}

// QueryDSL 설정
tasks.withType(JavaCompile) {
	options.generatedSourceOutputDirectory = querydslGeneratedDir
}

// Q클래스 자동 삭제 설정
clean {
	doLast {
		delete querydslGeneratedDir
	}
}